* MY FIRST REST FUNCTION

* Set our response to application/json

CALL WSETCONTENTTYPE("application/json")

* Turn off cors logic (development only)

CALL WSETHEADER("Access-Control-Allow-Origin","*")

* Get our request_method 

CALL WGETHEADER(REQUEST.METHOD,"REQUEST_METHOD")

* Special code for CORS.  If cors kicks and a OPTION request is sent by the browser the
* following code will make this work.  This is a development only item.  For production
* you want to read the CORS documents.

IF REQUEST.METHOD = "OPTIONS" THEN
	CALL WSETHEADER("Access-Control-Headers"		,"Content-Type, Authentication"				)
	CALL WSETHEADER("Access-Control-Allow-Headers"	,"Content-Type, X-AUTHENTICATION, X-API-KEY, Cookies"	)
	CALL WSETHEADER("Access-Control-Allow-Methods"	,"GET,PUT,POST,DELETE"						)
	CALL WSEND("")
	STOP
END

* Build a blank response object to send back

CALL WOBJ(RESPONSE.OBJ,"FROMSTRING","","{}","",RERR)

* Set our default response status and status message

JSON.STATUS="ok"
JSON.STATUS.MESSAGE=""

CALL WGETBODY(BODY)
OPEN "RESULT.FILE" TO RFILE ELSE STOP
WRITE BODY ON RFILE, "BODY"
CALL WOBJ(BODYOBJ,"FROMSTRING","",BODY,"",RERR)

CALL WOBJ(BODYOBJ,"GET","action",ACTION,"",RERR)
ACTION=OCONV(ACTION,"MCU")
IF ACTION="" THEN ACTION="LIST"

BEGIN CASE
    CASE ACTION="LIST"
        EXECUTE "TSORT SNAPON.PHANTOM.FILE pid command logon datetime program" CAPTURING RESULT
        IF RESULT[1,1]="[" THEN
            CALL WOBJ(RESPONSE.OBJ,"SET.ARRAY","results",RESULT,"",RERR)
            WRITE RESULT ON RFILE, "RESULTS"
        END ELSE
            JSON.STATUS="error"
            JSON.STATUS.MESSAGE="Invalid Response: ":RESULT
        END
    CASE ACTION=""
        JSON.STATUS="error"
        JSON.STATUS.MESSAGE="Invalid action ":ACTION
END CASE

end.of.program: *

* Builds our response status items and builds response json

CALL WOBJ(RESPONSE.OBJ,"SET.STRING","status",JSON.STATUS,"",RERR)
CALL WOBJ(RESPONSE.OBJ,"SET.STRING","statusmsg",JSON.STATUS.MESSAGE,"",RERR)
CALL WOBJ(RESPONSE.OBJ,"TOSTRING","",RESPONSE.JSON,"PRETTIFY",RERR)

CALL WSEND(RESPONSE.JSON)

STOP; * end of main program


